Clear 

new circuit.IEEE13Nodeckt 
~ basekv=115 pu=1.00 phases=3 bus1=SourceBus  
~ Angle=30                     ! advance angle 30 deg so result agree with published angle
~ MVAsc3=20000 MVASC1=21000    ! stiffen the source to approximate inf source

set earthmodel=carson

//New Transformer.Sub Phases=3 Windings=2   XHL=(8 1000 /)
//~ wdg=1 bus=SourceBus  conn=delta  kv=115  kva=5000   %r=(.5 1000 /)
//~ wdg=2 bus=650        conn=wye    kv=4.16 kva=5000   %r=(.5 1000 /)

// This adds an unloaded tertiary winding, but note GridLAB-D only supports 2 windings
New Transformer.Sub3 Phases=3 Windings=3   XHL=0.01 XHT=0.025 XLT=0.025
~ wdg=1 bus=SourceBus  conn=delta  kv=115  kva=5000   %r=(.5 1000 /)
~ wdg=2 bus=650.1.2.3.4 Rneut=0 Xneut=0.4 conn=wye    kv=4.16 kva=5000   %r=(.5 1000 /)
~ wdg=3 bus=650z       conn=wye    kv=13.2 kva=1000   %r=(.5 1000 /)

New Transformer.Reg1 phases=1 xhl=0.01 kvas=[1666 1666] kvs=[2.4 2.4] %LoadLoss=0.01 Bank=Reg Buses=[BRKR.1 RG60.1]
New Transformer.Reg2 phases=1 xhl=0.01 kvas=[1666 1666] kvs=[2.4 2.4] %LoadLoss=0.01 Bank=Reg Buses=[BRKR.2 RG60.2]
New Transformer.Reg3 phases=1 xhl=0.01 kvas=[1666 1666] kvs=[2.4 2.4] %LoadLoss=0.01 Bank=Reg Buses=[BRKR.3 RG60.3]

new regcontrol.Reg1  transformer=Reg1 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 
new regcontrol.Reg2  transformer=Reg2 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 
new regcontrol.Reg3  transformer=Reg3 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

New Transformer.XFM1  Phases=3   Windings=2  XHL=2
~ wdg=1 bus=XF1 conn=Wye kv=4.16    kva=500    %r=.55
~ wdg=2 bus=634 conn=Wye kv=0.480   kva=500    %r=.55

// these are local matrix line codes
New linecode.mtx601 nphases=3 BaseFreq=60 
~ rmatrix = (0.3465 | 0.1535 0.3375 | 0.1580 0.1560 0.3414 ) 
~ xmatrix = (1.0179 | 0.3849 1.0478 | 0.4236 0.5017 1.0348 ) 
~ cmatrix = (0 | 0 0 | 0 0 0) 
~ units=mi normamps=730
New linecode.mtx602 nphases=3 BaseFreq=60 
~ rmatrix = (0.7526 | 0.1535 0.7475 | 0.1560 0.1580 0.7436 ) 
~ xmatrix = (1.1814 | 0.3849 1.1983 | 0.5017 0.4236 1.2112 ) 
~ cmatrix = (0 | 0 0 | 0 0 0) 
~ units=mi normamps=340
New linecode.mtx603 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ cmatrix = (0 | 0 0) 
~ units=mi normamps=230
New linecode.mtx604 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ cmatrix = (0 | 0 0) 
~ units=mi normamps=230
New linecode.mtx605 nphases=1 BaseFreq=60 
~ rmatrix = (1.3292 ) 
~ xmatrix = (1.3475 )
~ cmatrix = (0) 
~ units=mi normamps=230
New linecode.mtx606 nphases=3 BaseFreq=60 
~ Rmatrix=[0.791721  |0.318476  0.781649  |0.28345  0.318476  0.791721  ]
~ Xmatrix=[0.438352  |0.0276838  0.396697  |-0.0184204  0.0276838  0.438352  ]
~ Cmatrix=[383.948  |0  383.948  |0  0  383.948  ]
~ units=mi normamps=260
New linecode.mtx607 nphases=1 BaseFreq=60 
~ rmatrix = (1.3425 ) 
~ xmatrix = (0.5124 )
~ cmatrix = [236] 
~ units=mi normamps=165

New Load.671 Bus1=671      Phases=3 Conn=Delta Model=1 kV=4.16  kW=1155 kvar=660 
New Load.634a Bus1=634.1   Phases=1 Conn=Wye   Model=1 kV=0.277 kW=160  kvar=110 
New Load.634b Bus1=634.2   Phases=1 Conn=Wye   Model=1 kV=0.277 kW=120  kvar=90 
New Load.634c Bus1=634.3   Phases=1 Conn=Wye   Model=1 kV=0.277 kW=120  kvar=90 
New Load.645 Bus1=645.2    Phases=1 Conn=Wye   Model=1 kV=2.4   kW=170  kvar=125 
New Load.646 Bus1=646.2.3  Phases=1 Conn=Delta Model=2 kV=4.16  kW=230  kvar=132 
New Load.692 Bus1=692.3.1  Phases=1 Conn=Delta Model=5 kV=4.16  kW=170  kvar=151 
New Load.675a Bus1=675.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=485  kvar=190 
New Load.675b Bus1=675.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=68   kvar=60 
New Load.675c Bus1=675.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=290  kvar=212 
New Load.611 Bus1=611.3    Phases=1 Conn=Wye   Model=5 kV=2.4   kW=170  kvar=80 
New Load.652 Bus1=652.1    Phases=1 Conn=Wye   Model=2 kV=2.4   kW=128  kvar=86 
New Load.670a Bus1=670.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=17   kvar=10 
New Load.670b Bus1=670.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=66   kvar=38 
New Load.670c Bus1=670.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=117  kvar=68 

New Capacitor.Cap1 Bus1=675 phases=3 kVAR=600 kV=4.16 
New Capacitor.Cap2 Bus1=611.3 phases=1 kVAR=100 kV=2.4 

!Bus 670 is the concentrated point load of the distributed load on line 632 to 671 located at 1/3 the distance from node 632

New Line.650632    Phases=3 Bus1=RG60.1.2.3   Bus2=632.1.2.3  LineCode=mtx601 Length=2000 units=ft 
New Line.632670    Phases=3 Bus1=MID.1.2.3    Bus2=670.1.2.3  LineCode=mtx601 Length=667  units=ft    
New Line.670671    Phases=3 Bus1=670.1.2.3    Bus2=671.1.2.3  LineCode=mtx601 Length=1333 units=ft 
New Line.671680    Phases=3 Bus1=671.1.2.3    Bus2=680.1.2.3  LineCode=mtx601 Length=1000 units=ft 
New Line.632633    Phases=3 Bus1=632.1.2.3    Bus2=633.1.2.3  LineCode=mtx602 Length=500  units=ft 
New Line.632645    Phases=2 Bus1=632.3.2      Bus2=645.3.2    LineCode=mtx603 Length=500  units=ft 
New Line.645646    Phases=2 Bus1=645.3.2      Bus2=646.3.2    LineCode=mtx603 Length=300  units=ft 
New Line.692675    Phases=3 Bus1=692.1.2.3    Bus2=675.1.2.3  LineCode=mtx606 Length=500  units=ft 
New Line.671684    Phases=2 Bus1=671.1.3      Bus2=684.1.3    LineCode=mtx604 Length=300  units=ft 
New Line.684611    Phases=1 Bus1=TAP.3        Bus2=611.3      LineCode=mtx605 Length=300  units=ft 
New Line.684652    Phases=1 Bus1=684.1        Bus2=652.1      LineCode=mtx607 Length=800  units=ft 

New Line.671692    Phases=3 Bus1=671  Bus2=692  Switch=y r1=1e-4 r0=1e-4 x1=0 x0=0 c1=0 c0=0

// additional switches for Breaker, Fuse, Recloser and (placeholder) Sectionaliser (CIM spelling)
New Line.Brkr1     Phases=3 Bus1=650    Bus2=BRKR   Switch=y r1=1e-4 r0=1e-4 x1=0 x0=0 c1=0 c0=0
New Relay.Brkr1    MonitoredObj=Line.Brkr1 Type=Current PhaseTrip=1200.0 GroundTrip=600.0 Delay=0.1
New Line.Fuse1     Phases=3 Bus1=633    Bus2=XF1    Switch=y r1=1e-4 r0=1e-4 x1=0 x0=0 c1=0 c0=0
New Fuse.Fuse1     MonitoredObj=Line.Fuse1 RatedCurrent=100.0
New Line.Rec1      Phases=3 Bus1=632    Bus2=MID    Switch=y r1=1e-4 r0=1e-4 x1=0 x0=0 c1=0 c0=0
New Recloser.Rec1  MonitoredObj=Line.Rec1 PhaseTrip=1200.0 GroundTrip=400.0
New Line.Sect1     Phases=1 Bus1=684.3  Bus2=TAP.3  Switch=y rmatrix=(1e-4) xmatrix=(0) cmatrix=(0)
// New Sectionlizer.Sect1 MonitoredObj=Line.Sect1 PickupCurrent=100.0 Counts=2

// additional service transformer and DER components for testing
New PVSystem.School phases=3 bus1=634 kV=0.48 kVA=300 irrad=0.8 Pmpp=300 PF=1
New Storage.School  phases=3 bus1=634 kV=0.48 kWRated=100 kWhRated=200 kWhStored=100 state=discharging 
New Storage.BatIdle  phases=3 bus1=634 kV=0.48 kWRated=100 kWhRated=200 kWhStored=100 state=idling

New XfmrCode.CT5  phases=1 windings=3 kvs=[2.4 0.12 0.12] kVAs=[5.0 5.0 5.0] 
~ %imag=0.5 %Rs=[0.6 1.2 1.2] %noloadloss=0.2 Xhl=2.04 Xht=2.04 Xlt=1.36
New Transformer.Tpoletop XfmrCode=CT5 buses=[670.2 House.1.0 House.0.2]
New Load.House     phases=2 Bus1=House.1.2 kv=0.208 conn=wye model=1 kW=5.0 pf=0.85 Vminpu=.88
New PVSystem.House phases=2 bus1=House.1.2 kv=0.208 conn=wye kVA=5 irrad=0.8 Pmpp=5 PF=1 
New Storage.House  phases=2 bus1=House.1.2 kv=0.208 conn=wye kWRated=5 kWhRated=13.5 kWhStored=5 state=charging 

Set Voltagebases=[115, 4.16, 13.2, 0.48, 0.208]
calcv
Solve
BusCoords IEEE13NodeExtra_BusXY.csv

Transformer.Reg1.Taps=[1.0 1.0625]
Transformer.Reg2.Taps=[1.0 1.0500]
Transformer.Reg3.Taps=[1.0 1.06875]
Set Controlmode=OFF

Solve
